module comafnmr

!  some global variables go here

   implicit none
   integer,parameter::MAXAT=60000,MAXRES=20000,MAXIQM=600
   double precision::coord(3,MAXAT), fxyz(3,MAXIQM)
   double precision:: qmcharge(MAXAT),rad(MAXAT)
   logical::connect(MAXRES,MAXRES)
   logical::atomsign(MAXAT)
   logical::ter(0:MAXRES)
   character(len=1):: restype(MAXRES)
   character(len=2):: element(MAXAT),dummyl
   character(len=3):: residue(MAXAT), residuename(MAXRES)
   character(len=4):: atomname(MAXAT), atomname_uniq(MAXAT), tmpnam
   character(len=5):: dlabel(MAXAT)
   character(len=6):: cfragtxt
   character(len=160):: commandline, line, afnmrhome
   character(len=30) :: pqrstart, atombasis(MAXIQM)
   character(len=24) :: pqrend
   character(len=5) functional
   character(len=:), allocatable :: filek, version
   character(len=1) :: basis
!     here resno() goes sequentially from 1 to nres;
!          resno_user() are the residue numbers in the input pdb file
!          (note: does not recognize chainID's, so all resno_user() values
!           should be unique)
   integer ::resno(MAXAT), resno_user(MAXAT), list(MAXRES), prev_resno_user
   integer :: modnum, ier, listsize, lengthb, natom, nhighatom, nlowatom
   integer :: firstprotres, lastprotres, lastprotatom, iatfinal, iatstart
   logical :: gaussian, orca, demon, demon5, qchem, jaguar, terachem, &
              qopt, xtb, sqm, quick, spinspin
   logical :: first=.true.
   logical :: nofrag=.false.
   double precision :: nbcut

end module

program afnmr_x

!   (Note: generally the input for afnmr.x is created by the shell script
!      "afnmr", which is in the AFNMRHOME/bin directory.)
!
!   Usage: afnmr.x program basis qopt functional nbcut spinspin basename list
!
!      where program  is G (Gaussian), O (Orca), D (Demon v3,4), E (Demon v5),
!                     Q (Qchem), J (Jaguar), or S (sqm)
!            basis is D (double-zeta) or T (triple-zeta) 
!                  or M (primary res. Q, rest T), A (aug-tzp), or S (shape)
!            qopt controls off quantum geometry optimization: set to
!                  E/G/O/X/Q/T (for optimization with demon, Gaussian, ORCA, xtb, 
!                  quick or terachem), or use F (false, default) to turn off
!            functional is a string giving the desired DFT functional
!            nbcut is the heavy-atom nonbonded cutoff for fragment creation
!            spinspin is T for J-couplings, F (default) otherwise
!            <basename>.pqr gives the input structure; put all "general"
!                  residues (water, ligands, etc.) after protein or
!                  nucleic acid residues
!            list is a list of residues to fragment; if empty, do all
!                residues up to lastprotres
!
      use comafnmr
      implicit none

      double precision :: x,y,z,tempdis
      character(len=8) :: lpchar
      character(len=1) :: program,qoptb,spinspinb
      integer :: selectC(0:MAXRES+2),charge(MAXRES),cfrag
      integer :: selectCA(MAXRES+2), resmap(MAXRES)
      integer :: i,j,k,kk,m,iitemp,iqm,iqmprot=0
      integer :: kfinal,ktemp,kstart,kcount,kbas,kuser
      integer :: n1,n2,nprotc,nres,nsf,nptemp
      character(len=3) :: rn, kuser_c
      character(len=6) :: nbcutb

      double precision dis
      double precision chargef(MAXRES)
      character(len=80) basename
      character(len=:), allocatable :: pdbfile
      integer lengthc,iline
#ifdef __INTEL_COMPILER
      integer system
#endif

      integer, parameter ::MAXNRES=33,MAXPRES=43
      character(len=3) :: nresn(MAXNRES), presn(MAXPRES)

      nresn(1) = '  G'
      nresn(2) = '  A'
      nresn(3) = '  U'
      nresn(4) = '  C'
      nresn(5) = ' DG'
      nresn(6) = ' DA'
      nresn(7) = ' DT'
      nresn(8) = ' DC'
      nresn(9) = 'GUA'
      nresn(10) = 'ADE'
      nresn(11) = 'THY'
      nresn(12) = 'CYT'
      nresn(13) = 'URA'
      nresn(14) = ' G5'
      nresn(15) = ' A5'
      nresn(16) = ' U5'
      nresn(17) = ' C5'
      nresn(18) = 'DG5'
      nresn(19) = 'DA5'
      nresn(20) = 'DT5'
      nresn(21) = 'DC5'
      nresn(22) = ' G3'
      nresn(23) = ' A3'
      nresn(24) = ' U3'
      nresn(25) = ' C3'
      nresn(26) = 'DG3'
      nresn(27) = 'DA3'
      nresn(28) = 'DT3'
      nresn(29) = 'DC3'
      nresn(30) = 'gtp'
      nresn(31) = 'DMA'
      nresn(32) = 'M6A'
      nresn(33) = 'CYH'

      presn(1) = 'ALA'
      presn(2) = 'ARG'
      presn(3) = 'ASH'
      presn(4) = 'ASN'
      presn(5) = 'ASP'
      presn(6) = 'CYS'
      presn(7) = 'CYX'
      presn(8) = 'GLH'
      presn(9) = 'GLN'
      presn(10) = 'GLU'
      presn(11) = 'GLY'
      presn(12) = 'HID'
      presn(13) = 'HIE'
      presn(14) = 'HIP'
      presn(15) = 'ILE'
      presn(16) = 'LEU'
      presn(17) = 'LYN'
      presn(18) = 'LYS'
      presn(19) = 'MET'
      presn(20) = 'PHE'
      presn(21) = 'PRO'
      presn(22) = 'SER'
      presn(23) = 'THR'
      presn(24) = 'TRP'
      presn(25) = 'TYR'
      presn(26) = 'VAL'
      presn(27) = 'HIS'
      presn(28) = 'ACE'
      presn(29) = 'NHE'
      presn(30) = 'NME'
      presn(31) = 'CYB'
      presn(32) = 'PTR'
      presn(33) = 'SEP'
      presn(34) = 'TPO'
      presn(35) = 'Y1P'
      presn(36) = 'S1P'
      presn(37) = 'T1P'
      presn(38) = 'H1D'
      presn(39) = 'H2D'
      presn(40) = 'H1E'
      presn(41) = 'H2E'
      presn(42) = 'EEY'
      presn(43) = 'BTF'

      gaussian = .false.
      orca = .false.
      qchem = .false.
      demon = .false.
      demon5 = .false.
      terachem = .false.
      jaguar = .false.
      xtb = .false.
      sqm = .false.
      quick = .false.
      qopt = .false.
      spinspin = .false.
      listsize = 0
      version = '1.9'

      write(6,*)
      write(6,*)'**********************************************'
      write(6,*)'AF-NMR'
      write(6,*)
      write(6,*)'Authors : Xiao He, Kenneth Merz,'
      write(6,*)'          Sishi Tang and David A. Case'
      write(6,*)' Version: ', trim(version)
      write(6,*)'**********************************************'

      call get_environment_variable('AFNMRHOME', afnmrhome )

      if( command_argument_count() .lt. 5 ) then
         write(6,*) &
       'Usage: af-nmr program basis qopt <basename> {list}'
         stop 1
      end if

      call get_command_argument( 1, program, lengthb )
      if( program .eq. 'G' ) then
         gaussian = .true.
      else if( program .eq. 'D' ) then
         demon = .true.
      else if( program .eq. 'E' ) then
         demon = .true.
         demon5 = .true.
      else if( program .eq. 'O' ) then
         orca = .true.
      else if( program .eq. 'Q' ) then
         qchem = .true.
      else if( program .eq. 'J' ) then
         jaguar = .true.
      else if( program .eq. 'S' ) then
         sqm = .true.
      else
         write(0,*) 'Bad input for program: ', program
         stop 1
      endif

      call get_command_argument( 2, basis, lengthb )

      call get_command_argument( 3, qoptb, lengthb )
      if( qoptb .eq. 'D' .or. qoptb .eq. 'E' .or. qoptb .eq. 'G' &
                         .or. qoptb .eq.  'O' ) then
         qopt = .true.
      else if (qoptb .eq. 'X' ) then
         xtb = .true.
      else if (qoptb .eq. 'Q' ) then
         quick = .true.
      else if (qoptb .eq. 'T' ) then
         terachem = .true.
      else if (qoptb .ne. 'F' ) then
         write(0,*) 'Bad input for qopt: ', qoptb
         stop 1
      endif

      call get_command_argument( 4, functional, lengthb )

      call get_command_argument( 5, nbcutb, lengthb )
      read( nbcutb, * ) nbcut

      call get_command_argument( 6, spinspinb, lengthb )
      if( spinspinb .eq. 'T' ) spinspin = .true.

      call get_command_argument( 7, basename, lengthb )
      pdbfile = basename(1:lengthb) // '.pqr'

      if( command_argument_count() > 7 ) then
         do i=8,command_argument_count()
            call get_command_argument( i, lpchar, lengthc )
            read( lpchar, '(i4)' ) list(i-7)
         end do
         listsize = command_argument_count() - 7
      endif
      if( listsize .eq. 1 .and. list(1) .eq. 0 ) then 
         nofrag = .true.
         write(6,'(a)') 'Setting nofrag to .true.; fragmentation skipped'
      end if

      write(6,'(a,a)') 'Running afnmr, version ',trim(version)
      write(6,'(a,a1,1x,a1,1x,a1,1x,a,1x,f6.2,1x,a,1x,a)') &
           'Input arguments: ', program, basis, qoptb, &
           functional, nbcut, spinspinb, trim(basename(1:lengthb))
      write(6,'(a,f6.3)') &
         'Fragments will be based on heavy atom contacts < ',nbcut

      ter(0:MAXRES) = .false.
      chargef(1:MAXRES) = 0.d0
!
!     Read the input pqr file:
!
      open(10,file=pdbfile)

      nptemp = 0
      i = 0
      prev_resno_user = 0
      do k=1,MAXAT
        read(10,'(a80)',end = 101) line
        if( line(1:5) .eq. 'ATOM ' .or. line(1:5) .eq. 'HETAT') then
           i = i + 1
           read(line,100)atomname(i),residue(i),  &
             resno_user(i),(coord(j,i),j=1,3),qmcharge(i),rad(i),element(i)
100        format(12x,a4,1x,a3,2x,I4,4x,3f8.3,f8.4,f8.3,6x,a2)
           ! skip extra points:
           if( element(i) .eq. ' E' .or. element(i) .eq. ' ' ) then
              i = i - 1
              cycle
           endif
           ! for nofrag, set all residue numbers to 1; make atom names unique
           if( nofrag ) then
              resno_user(i) = 1
              tmpnam = adjustl(atomname(i))
              write(atomname_uniq(i), '(a,i0.3)') tmpnam(1:1), i
           end if

           if( first ) then
              firstprotres = resno_user(i)
              first = .false.
           endif
           if( resno_user(i) .ne. prev_resno_user ) then ! found new residue
              nptemp = nptemp + 1
              prev_resno_user = resno_user(i)
              !  create list if user didn't enter one:
              if( listsize == 0 ) list(nptemp) = resno_user(i)
           endif
           resno(i) = nptemp
           resmap(resno_user(i)) = nptemp  ! maps user-resno to sequential ones
           chargef(nptemp) = chargef(nptemp) + qmcharge(i)
           if( nptemp.gt.MAXRES ) then
              write(0,*) 'too many residues: ', nptemp, MAXRES
              stop 1
           endif
           residuename(nptemp)=residue(i)

           rn = residue(i)
           restype(nptemp) = 'G'
           do m=1,MAXNRES
             if( rn.eq.nresn(m) ) restype(nptemp) = 'N'
           end do
           do m=1,MAXPRES
             if( rn.eq.presn(m) ) restype(nptemp) = 'P'
           end do
           if( rn.eq.'HOH' .or. rn.eq.'WAT' ) restype(nptemp) = 'W'

        else if( line(1:3) .eq. 'TER' ) then
!          mark residue nptemp as a terminal residue:
           ter(nptemp) = .true.
        endif
      end do
101   natom = i
      nres = max(1,nptemp)

      !  figure out the last protein residue:
      lastprotres = nres
      do i=1,nres
        if( restype(i) .eq. 'G' .or. restype(i) .eq. 'W' ) then
           lastprotres = i-1
           exit
        endif
      end do

      !  figure out last protein atom:
      do i=1,natom
         if( resno(i) .eq. lastprotres ) lastprotatom = i
      end do

!     lastprotres has to be a terminal residue:
      if( lastprotres > 0 ) ter(lastprotres) = .true.

      close(10)

!     real kludge, for pqr files with no element info and no 2-letter elements:
      do i=1,natom
        if( element(i).eq.'  ' ) then
           element(i)(1:1) = ' '
           element(i)(2:2) = atomname(i)(2:2)
        endif
      enddo
!
!     Populate arrays to identify C and CA atoms (for proteins); equivalent
!     atoms (O3' and C3') for nucleotides:
!
      do i=2,natom
        if( restype(resno(i)).eq.'P' ) then
           if (atomname(i).eq.' C  ')  selectC(resno(i))=i
           if (atomname(i).eq.' CA ')  selectCA(resno(i))=i
        else if( restype(resno(i)).eq.'N' ) then
           if (atomname(i).eq.' O3''')  selectC(resno(i))=i
           if (atomname(i).eq.' C3''')  selectCA(resno(i))=i
        else
           if ( resno(i-1).ne.resno(i) ) then
              selectC(resno(i))=i  ! selectC becomes first atom of a gen. res.
              selectC(resno(i-1)+1)=i  ! if residue numbers are not consecutive
           endif
        endif
      enddo
      if( residuename(lastprotres).eq.'NHE' .or. &
          residuename(lastprotres).eq.'NME' ) then
        selectC(lastprotres) = lastprotatom-2  !! will make kfinal=lastprotatom
      endif
      if( restype(1).eq.'G' .or. restype(1) .eq. 'W' ) selectC(1) = 1
      selectC(0) = 1
      selectC(nres+1) = natom+1
!
!     Set up the connectivity arrays:
!
      connect(1:nres,1:nres)=.false.
      do i=1,nres
        connect(i,i)=.true.
      enddo

      charge(1:nres) = nint( chargef(1:nres) )

!----------------------------------------------------------------------------

      do i=1,natom
        if( element(i).eq.' H' ) cycle

        do j=i+1,natom

            if( resno(i).eq.resno(j) ) cycle
            if( element(j).eq.' H' ) cycle
            if( element(j).eq.'Na' ) cycle

            dis=dsqrt((coord(1,i)-coord(1,j))**2   &
             +(coord(2,i)-coord(2,j))**2+(coord(3,i)-coord(3,j))**2)
!
!            nonbond distance < nbcut between heavy atom pairs, or
!                 less than 1.4*nbcut if atom j is a water oxygen:
!
            if( dis.le.nbcut .or. &
                 ( residue(j).eq.'WAT' .and. dis.le.1.4*nbcut ) .or. &
                 ( residue(j).eq.'HOH' .and. dis.le.1.4*nbcut ) ) then 

                connect(resno(i),resno(j))=.true.
                connect(resno(j),resno(i))=.true.
!
!               need to make sure that next residue is also connected if 
!               one of the atoms is beyond selectC:
!
                if( i.ge.selectC(resno(i)) .and. &
                restype(resno(i)).ne.'G' .and. restype(resno(i)).ne.'W') then
                   connect(resno(i)+1,resno(j))=.true.
                   connect(resno(j),resno(i)+1)=.true.
                endif
                if( j.ge.selectC(resno(j)) .and. &
                restype(resno(j)).ne.'G' .and. restype(resno(j)).ne.'W') then
                   connect(resno(i),resno(j)+1)=.true.
                   connect(resno(j)+1,resno(i))=.true.
                endif
            endif

        enddo  !  j=i+1,natom
      enddo    !  i=1,natom
!
!     Big loop over residues to create fragments:
!
      do kcount=1,nres

        if( listsize > 0 ) then
           if( kcount > listsize ) exit ! done with input list
        else
           if( kcount > lastprotres ) exit  ! done with default list
        endif
        ! We need both "kuser" (user's residue number) and "k" (a
        !    sequential residue number:
        kuser = list( kcount )
        if( kuser .eq. 0 ) kuser = 1  ! handle nofrag option
        k = resmap(kuser)

        iqm = 0   !  keep track of the atom number in the output file
        modnum = 0  ! get unique names for added hydrogens

        write(6,*)
        write(6,'(a,i4,a)') '==== Residue ',kuser, &
           ' ==========================================================='
        if( nofrag) then
           filek = basename(1:lengthb)
        else
           write(kuser_c,'(i0.3)') kuser
           filek = basename(1:lengthb)//kuser_c
        end if

        call write_header_info( kuser )

        atomsign(1:natom)=.false. ! flags whether atom is in the quantum region

!       cycle through all "connected" fragments to get the charge on the
!       quantum region (cfrag), and to mark each quantum atom by atomsign(i)

        cfrag = 0
        do ktemp=1,nres
          if(connect(k,ktemp))then
            call get_atom_range( kstart, kfinal, ktemp, &
                   selectC, restype(ktemp), ter)
            atomsign(kstart:kfinal) = .true.
            cfrag = cfrag + charge(ktemp)
            write(6,'(20x,i5,a4,5i6)') kuser,residuename(ktemp), &
                 resno_user(kfinal),kstart,kfinal,charge(ktemp),cfrag
          endif
        enddo
        write(6,'(2i5)') kuser, cfrag
        call write_cfrag_header_info(cfrag)

        nhighatom=0
        nlowatom=0
!
!       get starting, ending atoms for residue "k":
!
        call get_atom_range( iatstart, iatfinal, k, &
            selectC, restype(k), ter )
!
!       write out atoms in principal residue:
!
        do kk=iatstart,iatfinal
          iqm = iqm + 1
          nhighatom = nhighatom + 1
          call addatom( kk, iqm, .true. )
        enddo

        do ktemp=1,nres
          if( ktemp.ne.k .and. connect(k,ktemp) )then

              call get_atom_range( kstart, kfinal, ktemp, &
                   selectC, restype(ktemp), ter)
!
!             write out coordinates in a "connected" residue
!
              do kk=kstart,kfinal
                nlowatom = nlowatom + 1
                iqm = iqm + 1
                call addatom( kk, iqm, .false. )
              enddo
!
!             add hydrogens to dangling residues:
!
              if(ktemp.ne.1 .and. ktemp.le.lastprotres  &
                  .and. .not. ter(ktemp-1) )then
!
!               ---look for "backwards" links to previous residue:
!
                if(.not. atomsign(kstart-1) )then
                  n1=kstart
                  n2=selectCA(ktemp-1)
                  if(atomname(n1).eq.' C  ') then
                    call xyzchange(coord(1,n2),coord(2,n2),coord(3,n2),  &
                      coord(1,n1),coord(2,n1),coord(3,n1),x,y,z)
                  else
                    call xyzchangeO(coord(1,n2),coord(2,n2),coord(3,n2), &
                      coord(1,n1),coord(2,n1),coord(3,n1),x,y,z)
                  endif
                  nlowatom = nlowatom + 1
                  iqm = iqm + 1
                  call addH( iqm, x, y, z )
                endif
              endif

              if(ktemp.lt.lastprotres .and. .not. ter(ktemp)) then
!
!             ---look for "forwards" links to the next residue:
!
                if(.not. atomsign(kfinal+3) )then
                  n1=selectCA(ktemp)
                  n2=selectC(ktemp)
                  call xyzchange(coord(1,n2),coord(2,n2),coord(3,n2),  &
                    coord(1,n1),coord(2,n1),coord(3,n1),x,y,z)
                  nlowatom = nlowatom + 1
                  iqm = iqm + 1
                  call addH( iqm, x, y, z )
                endif
              endif

          endif
          if( ktemp == lastprotres ) iqmprot = iqm  ! only freeze protein
        enddo  ! ktemp

        !  check for dangling S--S bonds:

        do kk=1,natom
          if( atomname(kk)(2:3) == 'SG' .and. atomsign(kk) ) then
!           write(0,*) 'found SG: ', resno(kk), atomsign(kk)
            do i=1,natom
              if( i.eq.kk) cycle
              if( atomname(i)(2:3) .ne. 'SG') cycle
              tempdis=dsqrt((coord(1,i)-coord(1,kk))**2.0d0  &
                 +(coord(2,i)-coord(2,kk))**2.0d0            &
                 +(coord(3,i)-coord(3,kk))**2.0d0)
              if(tempdis.le.2.5d0 .and. kk.ne.i   &
                   .and.  .not. atomsign(i) )then
                !we have found a "dangling" S--S bond; saturate:
                call xyzchangeS(coord(1,i),coord(2,i),coord(3,i),  &
                    coord(1,kk),coord(2,kk),coord(3,kk),x,y,z)
                iqm = iqm + 1
                call addH( iqm, x, y, z)
              endif
            enddo
          endif
        enddo
        close(31)

        call handle_external_charges()
        call finish_program_files( iqm, iqmprot )
        call external_minimizer( cfrag, iqm, kuser )

        write(0,'(a,i4)') '    done with residue ', kuser

      enddo  ! big loop over residues

end program afnmr_x

subroutine xyzchange(xold,yold,zold,xzero,yzero,zzero,xnew,ynew,znew)

        implicit none
        double precision xold,yold,zold,xzero,yzero,zzero,xnew,ynew,znew,grad

         grad=dsqrt(1.09d0**2/((xold-xzero)**2+(yold-yzero)**2   &
           +(zold-zzero)**2))
         xnew=xzero+grad*(xold-xzero)
         ynew=yzero+grad*(yold-yzero)
         znew=zzero+grad*(zold-zzero)
         return
end subroutine xyzchange

subroutine xyzchangeS(xold,yold,zold,xzero,yzero,zzero,xnew,ynew,znew)

        implicit none
        double precision xold,yold,zold,xzero,yzero,zzero,xnew,ynew,znew,grad

         grad=dsqrt(1.33d0**2/((xold-xzero)**2+(yold-yzero)**2   &
           +(zold-zzero)**2))
         xnew=xzero+grad*(xold-xzero)
         ynew=yzero+grad*(yold-yzero)
         znew=zzero+grad*(zold-zzero)
         return
end subroutine xyzchangeS

subroutine xyzchangeO(xold,yold,zold,xzero,yzero,zzero,xnew,ynew,znew)

        implicit none
        double precision xold,yold,zold,xzero,yzero,zzero,xnew,ynew,znew,grad

         grad=dsqrt(0.96d0**2/((xold-xzero)**2+(yold-yzero)**2  &
           +(zold-zzero)**2))
         xnew=xzero+grad*(xold-xzero)
         ynew=yzero+grad*(yold-yzero)
         znew=zzero+grad*(zold-zzero)
         return
end subroutine xyzchangeO

subroutine get_atom_range( kstart, kfinal, ktemp,  &
           selectC, restype, ter )
        implicit none

        integer, intent(in)   :: ktemp, selectC(0:*)
        integer, intent(out)  :: kstart, kfinal
        logical, intent(in)   :: ter(0:*)
        character(len=1), intent(in) ::  restype

        if( restype.eq.'P' ) then
          kstart=selectC(ktemp-1)
          kfinal=selectC(ktemp)-1
          if( ter(ktemp) )then
            kfinal=selectC(ktemp)+2
          endif
          if( ter(ktemp-1) ) then
            kstart=selectC(ktemp-1)+3
          endif
        else if( restype.eq.'N' ) then
          kstart=selectC(ktemp-1)
          kfinal=selectC(ktemp)-1
          if( ter(ktemp) )then
            kfinal=selectC(ktemp)+1
          endif
          if( ter(ktemp-1) ) then
            kstart=selectC(ktemp-1)+2
          endif
        else    !  general (G/W) residue type: should be ligand or water
          kstart=selectC(ktemp)
          kfinal=selectC(ktemp+1)-1
        endif

        return
end subroutine get_atom_range

subroutine addatom( kk, iqm, principal )

      use comafnmr
      implicit none
      integer, intent(in) ::  kk,iqm
      logical, intent(in) :: principal
      integer :: j, atno
      character(len=3) ::  i_char
      character(len=1) ::  elem

      if ( demon .or. jaguar ) then
        write( i_char, '(i3)' ) iqm
        dlabel(iqm) = trim(element(kk)) // adjustl(i_char)
        if( len_trim(element(kk)) == 1 ) dlabel(iqm)(5:5) = ' ' 
        write(30,'(a,2x,3f12.5)') dlabel(iqm),(coord(j,kk),j=1,3)
      else if ( terachem ) then
        write(34,'(a2,4x,3f10.4)')element(kk),(coord(j,kk),j=1,3)
      else if ( gaussian .or. orca ) then
        write(30,'(a2,4x,3f10.4)')element(kk),(coord(j,kk),j=1,3)
        dlabel(iqm) = adjustl(element(kk))
        if( orca .and. basis .eq. 'M' .and. principal) then
          if( spinspin ) then
             write(30,'(a)') 'NewGTO "pcJ-2" end;'
          else
             write(30,'(a)') 'NewGTO "pcSseg-2" end;'
          end if
        end if
        if( gaussian .and. basis .eq. 'M' ) then
           if( principal ) then
              if( spinspin ) then
                 atombasis(iqm) = '/basis/pcJ-2/'
              else
                 atombasis(iqm) = '/basis/pcSseg-2/'
              end if
           else
              atombasis(iqm) = '/basis/pcSseg-1/'
           end if
           write(6,*) 'atombasis: ', iqm, trim(atombasis(iqm)), dlabel(iqm)
        end if
      else if ( sqm ) then
        elem = element(kk)(2:2)
        if ( elem == 'H' ) atno = 1
        if ( elem == 'C' ) atno = 6
        if ( elem == 'N' ) atno = 7
        if ( elem == 'O' ) atno = 8
        if ( elem == 'F' ) atno = 9
        if ( elem == 'P' ) atno = 15
        if ( elem == 'S' ) atno = 16
        write(30,'(i2,2x,a2,4x,3f10.4)') atno, element(kk),(coord(j,kk),j=1,3)
      else
        write(30,'(a2,4x,3f10.4)')element(kk),(coord(j,kk),j=1,3)
        dlabel(iqm) = trim(element(kk))
      endif
      if( xtb .or. quick ) then
        write(44,'(a2,4x,3f10.4)')element(kk),(coord(j,kk),j=1,3)
      endif

      if( nofrag ) then
         write(31,'(a,i5,1x,a4,1x,a3,i6,4x,3f8.3,f8.4,f8.3,6x,a2)') 'ATOM  ', &
           kk,atomname_uniq(kk),residue(kk),resno_user(kk), &
           (coord(j,kk),j=1,3), qmcharge(kk),rad(kk),element(kk)
      else
         write(31,'(a,i5,1x,a4,1x,a3,i6,4x,3f8.3,f8.4,f8.3,6x,a2)') 'ATOM  ', &
           kk,atomname(kk),residue(kk),resno_user(kk),(coord(j,kk),j=1,3),   &
           qmcharge(kk),rad(kk),element(kk)
       end if

      return
end subroutine addatom

subroutine addH( iqm, x, y, z )

      use comafnmr
      implicit none
      integer, intent(in) :: iqm
      double precision, intent(in) ::  x,y,z
      character(len=3) i_char

      if( demon .or. jaguar ) then
        write( i_char, '(i3)' ) iqm
        dlabel(iqm) = ' H' // adjustl(i_char)
        write(30,'(a,2x,3f12.5)') dlabel(iqm),x,y,z
      else if ( terachem ) then
        write(34,'(a2,4x,3f10.4)')'H ',x,y,z
      else if ( gaussian .or. orca ) then
        write(30,'(a2,4x,3f10.4)')' H',x,y,z
        dlabel(iqm) = 'H'
        if( gaussian .and. basis .eq. 'M' ) then
           atombasis(iqm) = '/basis/pcSseg-1/'
           write(6,*) 'atombasis: ', iqm, trim(atombasis(iqm)), dlabel(iqm)
        end if
      else if ( sqm ) then
        write(30,'(a2,4x,3f10.4)')' 1  H ',x,y,z
      else
        write(30,'(a2,4x,3f10.4)')' H ',x,y,z
      endif
      if( xtb .or. quick ) then
        write(44,'(a2,4x,3f10.4)')' H ',x,y,z
      end if

      modnum = modnum + 1
      ! assumes modum is never greater than 99:
      write(31,'(a,i5,2x,a,i0.2,a,3f8.3,f8.4,f8.3,6x,a2)') 'ATOM  ',  &
           iqm,'H',modnum,' MOD  9999    ',x,y,z,0.0,1.2,' H'

      return
end subroutine addH

subroutine transfer_minimized_coords(iqm)

      use comafnmr
      implicit none
      integer, intent(in) :: iqm
      integer :: i,j

      !  transfer the minimized coordinates to the .pqr file
      open(47,file=filek//'.pqr')
      open(48,file=filek//'.pqr1')
      do i=1,iqm
         read(47,'(a30,24x,a24)') pqrstart,pqrend
         write(48,'(a30,3f8.3,a16)') pqrstart, &
             fxyz(1,i), fxyz(2,i), fxyz(3,i), pqrend
      end do
      close(47)
      close(48)
      call execute_command_line( '/bin/mv ' // filek &
         // '.pqr1 ' // filek // '.pqr' )

      if( demon ) then

         !  transfer the minimized coordinates to the .inp file
         open(47,file=filek//'.inp')
         open(48,file=filek//'.inp1')
         do i=1,9999
            read(47,'(a)',end=106) line
            write(48,'(a)') trim(line)
            if( line(1:9) == 'GEOMETRY ' ) then
               do j=1,iqm
                  read (47,*) 
                  write(48,'(a,2x,3f12.5)') dlabel(j), &
                         fxyz(1,j), fxyz(2,j), fxyz(3,j)
               end do
            end if
         end do
  106    close(47)
         close(48)
         call execute_command_line( '/bin/mv ' // filek &
            // '.inp1 ' // filek // '.inp' )

      else if( orca ) then

         !  transfer the minimized coordinates to the .orcainp file
         open(47,file=filek//'.orcainp')
         open(48,file=filek//'.orcainp1')
         do i=1,9999
            read(47,'(a)',end=107) line
            write(48,'(a)') trim(line)
            if( line(1:5) == '* xyz' ) then
               do j=1,iqm
                  read (47,*) ! skip coordinate line from .orcainp file
                  write(48,'(a2,4x,3f10.4)') dlabel(j), &
                         fxyz(1,j), fxyz(2,j), fxyz(3,j)
                  if( basis .eq. 'M' .and. j .le. nhighatom ) then 
                      read (47,*) ! skip NewGTO line from .orcainp file
                      if( spinspin ) then
                         write(48,'(a)') 'NewGTO "pcJ-2" end;'
                      else
                         write(48,'(a)') 'NewGTO "pcSseg-2" end;'
                      end if
                  endif
               end do
            end if
         end do
  107    close(47)
         close(48)
         call execute_command_line( '/bin/mv ' // filek &
            // '.orcainp1 ' // filek // '.orcainp' )

      else if( gaussian ) then

         !  transfer the minimized coordinates to the .com file
         open(47,file=filek//'.com')
         open(48,file=filek//'.com1')
         do i=1,9999
            read(47,'(a)',end=108) line
            write(48,'(a)') trim(line)
            if( i == 7 ) then   ! TODO: fragile!
               do j=1,iqm
                  read (47,*) ! skip coordinate line from .com file
                  write(48,'(a2,4x,3f10.4)') dlabel(j), &
                         fxyz(1,j), fxyz(2,j), fxyz(3,j)
               end do
            end if
         end do
  108    close(47)
         close(48)
         call execute_command_line( '/bin/mv ' // filek &
            // '.com1 ' // filek // '.com' )

      else
         write(6,*) "external qopt only works with demon, gaussian or orca"
         call exit(1)
      end if
end subroutine transfer_minimized_coords

subroutine write_header_info(kuser)
      use comafnmr
      implicit none
      integer, intent(in) :: kuser
!
!       write header info:
!
        ! regular file opens here:
        if( gaussian ) then
          open(30,file=filek//'.com')
        else if ( sqm ) then
          open(30,file=filek//'.sqmin')
          ! open(35,file=filek//'.surf.pqr')
        else if ( orca ) then
          open(30,file=filek//'.orcainp')
          open(32,file=filek//'.pos')
        else if ( demon ) then
          open(30,file=filek//'.inp')
        else if ( qchem .or. jaguar ) then
          open(30,file=filek//'.in')
        end if

        ! qopt file opens here:
        if ( xtb ) then
          open(44,file=filek//'.xyz1')
          open(47,file=filek//'_xtb.inp')
        else if( quick ) then
          open(44,file=filek//'_quick.in')
        else if ( terachem ) then
          open(30,file=filek//'.opt')
          open(32,file=filek//'.pos')
          open(34,file=filek//'.xyz1')
        end if

        open(31,file=filek//'.pqr')
        open(33,file=filek//'.prot.pqr')

        ! regular header info here:
        if( gaussian ) then
          write(30,'(a)') '%mem=800mw'
          write(30,'(a)') '%nprocshared=4'
          write(30,'(a,a,a)', advance='no')  '# ', trim(functional), &
              '/Gen charge nosymm '
          if (qopt) then
            write(30,'(a)') 'Opt ReadOptimize '
          else if (basis .eq. 'S' ) then
            write(30,'(a)') 'Pop=HLY '
          else
            if( spinspin ) then
              write(30,'(a)') 'nmr(spinspin,printeigenvectors,readatoms) '
            else
              write(30,'(a)') 'nmr(printeigenvectors) '
            end if
          end if
          write(30,*)
          write(30,'(a,i4,a,a,a,f5.2)') ' AF-NMR fragment for residue ', &
               kuser, '; version = ',trim(version), '; nbcut = ', nbcut
          write(30,*)

        else if ( orca ) then
!         write(30,'(a)') '! PAL4'
          if( basis .eq. 'T' .or. basis .eq. 'M' ) then
            write(30,'(a,a,a)', advance='no') '! ', trim(functional), &
                ' pcSseg-1 '
          else if( basis .eq. 'Q' ) then
            write(30,'(a,a,a)', advance='no') '! ', trim(functional), &
                ' pcSseg-2 '
          else if( basis .eq. 'A' ) then
            write(30,'(a,a,a)', advance='no') '! ', trim(functional), &
                ' aug-pcSseg-1 '
          else if( basis .eq. 'D' ) then
            write(30,'(a,a,a)', advance='no') '! ', trim(functional), &
                ' pcSseg-0 '
          else
            write(0,*) 'Bad basis for orca: ', basis
            stop 1
          end if
          if( functional(2:2) .eq. '3' ) then
            write(30,'(a)', advance='no')  'AutoAux TightSCF RIJCOSX KDIIS '
          else
            write(30,'(a)', advance='no')  'TightSCF RI KDIIS '
          endif
          if( qopt ) write(30,'(a)', advance='no')  ' COpt '
          write(30,'(a)') ''
          if( functional(2:2) .eq. '3' ) then
            write(30,'(a)')  '%maxcore 3000'
          endif
          write(30,'(a)') ''
          write(30,'(a,a,a)') '%pointcharges "', filek, '.pos"'
          write(30,'(a)') ''
          write(30,'(a)') '%output'
          write(30,'(a)') '  PrintLevel Mini'
          write(30,'(a)') '  Print [ P_Cartesian ] 0'
          write(30,'(a)') '  Print [ P_Internal ] 0'
          write(30,'(a)') '  Print [ P_Basis ] 0'
          write(30,'(a)') '  Print [ P_Mulliken ] 0'
          write(30,'(a)') '  Print [ P_Loewdin ] 0'
          write(30,'(a)') '  Print [ P_Mayer ] 0'
          write(30,'(a)') '  Print [ P_OrbEn ] 0'
          write(30,'(a)') '  end'
          write(30,'(a)') ''
          write(30,'(a)') '%scf'
          write(30,'(a)') '  SOSCF Start 0.001 MaxIt 40 end'
          write(30,'(a)') '  DIISMaxEq 15'
          write(30,'(a)') '  directresetfreq 1'
          write(30,'(a)') '  end'
          write(30,'(a)') ''

        else if ( qchem ) then
          write(30,'(a)') '$comment'
          write(30,'(a,i4,a,a,a,f5.2)') ' AF-NMR fragment for residue ', &
               kuser, '; version = ',trim(version), '; nbcut = ', nbcut
          write(30,'(a)') '$end'
          write(30,'(a)') ''
          write(30,'(a)') '$molecule'

        else if ( jaguar ) then
          write(30,'(a,i4,a,a,a,f5.2)') ' AF-NMR fragment for residue ', &
               kuser, '; version = ',trim(version), '; nbcut = ', nbcut
          write(30,'(a)') '&gen'
          if ( basis .eq. 'D' ) write(30,'(a)') 'basis=6-31g'
          if ( basis .eq. 'T' ) write(30,'(a)') 'basis=6-311g**'
          write(30,'(a)') 'dftname=olyp'
          write(30,'(a)') 'igeopt=0'
          write(30,'(a)') 'nmr=1'

        else if ( demon ) then
          write(30,'(a,i4,a,a,a,f5.2)') 'TITLE AF-NMR fragment for residue ', &
               kuser, '; version = ',trim(version), '; nbcut = ', nbcut
          write(30,'(a)') 'SCFTYPE  RKS Tol=1.0e-7 MAX=100'
          write(30,'(a)') 'GUESS TB'
          write(30,'(a)') 'ORBITALS SPHERICAL'
          write(30,'(a)') 'ERIS MULTIPOLE'
          write(30,'(a,a)') 'VxcType Auxis ', trim(functional)
          write(30,'(a)') 'GRID FINE'
          write(30,'(a)') 'QUADRATURE RANDOM'
          write(30,'(a)') 'MIXING -0.1'
          write(30,'(a)') 'SHIFT 0.0'
          write(30,'(a)') 'DIIS ON'

        end if

        ! qopt headers here:
        if ( terachem ) then
          write(30,'(a)') 'basis 6-31gs'
          write(30,'(a,a)') 'coordinates ',filek//'.xyz'
          write(30,'(a,a)') 'pointcharges ',filek//'.pos'
          write(30,'(a,a)') 'scrdir ',filek
          write(30,'(a)') 'method pbe'
          write(30,'(a)') 'maxit 250'
          write(30,'(a)') 'levelshift yes'
          write(30,'(a)') 'levelshiftvala 0.2'
          write(30,'(a)') 'min_coordinates hdlc'
          write(30,'(a)') 'run minimize'
          write(30,'(a)') 'nstep 10'
          write(34,'(a)') 'put number of atoms here!'
          write(34,'(a)') filek
        end if
        return
end subroutine write_header_info

subroutine write_cfrag_header_info(cfrag)
      use comafnmr
      implicit none
      integer, intent(in) :: cfrag

        write(cfragtxt,'(i4)') cfrag
        !  write quantum chemistry files that depend on knowing cfrag:
        if ( gaussian .or. qchem ) then
          write(30,'(1x,I3,2x,I2)') cfrag,1
        else if ( orca ) then
          write(30,'(a,i3,1x,i3)')'* xyz ',cfrag,1
        else if ( jaguar ) then
          write(30,'(a,i3)')'molchg=',cfrag
          write(30,'(a)') '&'
          write(30,'(a)') '&zmat'
        else if ( demon ) then
          write(30,'(a,i3)')'CHARGE  ',cfrag
          write(30,'(a,i3)')'MULTIPLCITY  ',1
          write(30,'(a)') 'GEOMETRY CARTESIAN ANGSTROM'
        else if ( sqm ) then
          write(30, '(a)' ) filek
          write(30, '(a)' ) ' &qmmm'
          write(30, '(a,i3,a)' ) " qm_theory='AM1', qmcharge=", cfrag, &
                ', maxcyc=0, qmmm_int=1,'
          write(30, '(a)' ) ' /'
        end if

        !  Now a section for external qopt-only options
        if ( quick ) then
          write(44,'(a,a,a)') 'DFT OLYP BASIS=pcSseg-0 CHARGE=', &
                trim(adjustl(cfragtxt)), ' OPTIMIZE=10 EXTCHARGES'
          write(44,*)
        else if ( terachem ) then
          write(30,'(a,i3)')'charge  ',cfrag
          write(30,'(a)') 'end'
        endif
        return

end subroutine write_cfrag_header_info

subroutine handle_external_charges()
      use comafnmr
      implicit none
      double precision :: a,b,c,d
      integer :: iitemp,j,kk,nprotc,nsf

!       Deal with the charges representing the protein and solvent polarization:

        if( gaussian ) then
          write(30,*)
        else if ( qchem ) then
          write(30,'(a)') '$end'
          write(30,'(a)') ''
          write(30,'(a)') '$external_charges'
        else if ( jaguar ) then
          write(30,'(a)') '&'
          write(30,'(a)') '&pointchg'
        else if ( demon ) then
          write(30,'(a)') 'END'
          write(30,'(a)') 'EMBED READ'
        else if ( sqm ) then
          write(30,'(a)') '#EXCHARGES'
        endif

        if( quick ) write(44,*)

!       write out the protein positions and charges:

        do kk=1,natom
          if( .not. atomsign(kk) ) then
            if ( restype(resno(kk)).ne.'W' ) &
            write(33,'(a,i5,1x,a4,1x,a3,i6,4x,3f8.3,f8.4,f8.3,6x,a2)') &
               'ATOM  ', kk,atomname(kk),residue(kk),resno_user(kk), &
               (coord(j,kk),j=1,3), qmcharge(kk),rad(kk),element(kk)
          endif
        enddo

        close(33)
        call execute_command_line('./runsolinprot ' // filek, &
             exitstat = ier)
        if( ier .ne. 0 )then
           write(0,*) "error in solinprot: check solinprot.out"
           write(0,*) "error code was ", ier
           write(0,*) "command line was:"
           write(0,*) './runsolinprot ' // filek
           stop 1
        end if

        if( orca ) then
          nsf=0  ! number of surface points, just count them here
          open(23,file='srfchg.pos')
          do iitemp=1,999999
            read(23,*,end=59)a,b,c,d
            nsf = nsf + 1
          enddo
59        continue
          close(23)
          write(32,'(i7)') nsf
        endif

        open(23,file='srfchg.pos')
        do iitemp=1,999999
          read(23,*,end=60)a,b,c,d
          if( gaussian .or. qchem .or. demon ) then
            write(30,'(3f15.6,2x,f12.8)')a,b,c,d
          else if( jaguar ) then
            write(30,'(f12.8,2x,3f10.4)')d,a,b,c
          else if( orca .or. terachem ) then
            write(32,'(f12.8,2x,3f15.6)')d,a,b,c
          else if ( sqm ) then
            !  write out surface charge distributions, for visualization
            ! write(35,'(a,i5,1x,a4,1x,a3,i6,4x,3f8.3,f8.4,f8.2,6x,a2)') &
            !  'ATOM  ', iitemp,' H  ','SRF',1, a,b,c,d, 1.0, ' H'
            write(30, '(a,3f15.6,2x,f12.8)') ' 1  H ', a,b,c,d
          endif
          if( quick ) write(44,'(3f15.6,2x,f12.8)')a,b,c,d
        enddo
60      continue
        close(23)
        return
end subroutine handle_external_charges

subroutine finish_program_files( iqm, iqmprot )
      use comafnmr
      implicit none
      integer, intent(in) :: iqm, iqmprot
      integer :: i, kbas
      character(len=256) :: gbsname

        !  More program-dependent keywords and instructions:

        if( gaussian ) then
          write(30,*)
          if( basis .eq. 'M' ) then
            !    write local basis set
            do i=1,iqm
              write(30,'(i3,a)') i,' 0'
              gbsname = trim(afnmrhome) // trim(atombasis(i)) &
                    // trim(dlabel(i)) // '.gbs'
              write(6,*) 'finish: ', trim(gbsname)
              open( UNIT=11, FILE=trim(gbsname))
              rewind(11)
              do kbas=1,9999
                 read(11,'(a80)',end = 62) line
                 write(30,'(a)') trim(line)
              end do
              close(11)
62            write(30,'(a)') '****'
            end do

          else if( basis .eq. 'T' ) then
            open( UNIT=11, FILE=trim(afnmrhome) // &
                '/basis/pcsseg-1.1.gbs')
            rewind(11)
            do kbas=1,9999
               read(11,'(a80)',end = 63) line
               write(30,'(a)') trim(line)
            end do
            close(11)

          else if( basis .eq. 'D' ) then
            open( UNIT=11, FILE=trim(afnmrhome) // &
                '/basis/pcsseg-0.1.gbs')
            rewind(11)
            do kbas=1,9999
               read(11,'(a80)',end = 63) line
               write(30,'(a)') trim(line)
            end do
            close(11)

          else if( basis .eq. 'A' ) then
            open( UNIT=11, FILE=trim(afnmrhome) // &
                '/basis/aug-pcsseg-1.1.gbs')
            rewind(11)
            do kbas=1,9999
               read(11,'(a80)',end = 63) line
               write(30,'(a)') trim(line)
            end do
            close(11)

          else if( basis .eq. 'S' ) then
            open( UNIT=11, FILE=trim(afnmrhome) // &
                '/basis/pcseg-0.1.gbs')
            rewind(11)
            do kbas=1,9999
               read(11,'(a80)',end = 63) line
               write(30,'(a)') trim(line)
            end do
            close(11)

          else if( basis .eq. 'Q' ) then
            open( UNIT=11, FILE=trim(afnmrhome) // &
                '/basis/pcsseg-2.1.gbs')
            rewind(11)
            do kbas=1,9999
               read(11,'(a80)',end = 63) line
               write(30,'(a)') trim(line)
            end do
            close(11)

          else
            write(0,*) 'Bad basis for gaussian: ', basis
            stop 1

          endif
63        continue
          write(30,*)

          if (qopt) then
            if( nhighatom .lt. iqmprot ) then
               write(30,'(a,i0,a,i0,a,i0)') 'noatoms  atoms=1-', nhighatom, &
                    ', ', iqmprot+1, '-', natom
            else
               write(30,'(a,i0)') 'noatoms  atoms=1-', nhighatom
            end if
            write(30,*)
          endif

          if( spinspin .or. basis .eq. 'M' ) then
             write(30,'(a,i0)') ' atoms=1-', nhighatom
             write(30,*)
          end if

        else if ( sqm ) then
          write(30,'(a)') '#END'
        else if ( jaguar ) then
          write(30,'(a)') '&'
        else if ( demon ) then
          write(30,'(a)') 'END'
          if( nhighatom == 0 ) nhighatom = 4
          if( basis .eq. 'M' )then
            write(30,'(a)') 'BASIS  (DZVP)'
            do i=1,nhighatom
              write(30,'(a,a)') dlabel(i),' (pcSseg-2)'
            end do
            write(30,'(a)') 'AUXIS  (A2)'
            do i=1,nhighatom
              write(30,'(a,a)') dlabel(i),' (GEN-A2*)'
            end do
          else if (basis .eq. 'Q' ) then
            write(30,'(a)') 'BASIS  (pcSseg-2)'
            write(30,'(a)') 'AUXIS  (GEN-A2*)'
          else if (basis .eq. 'T' ) then
            write(30,'(a)') 'BASIS  (pcSseg-1)'
            write(30,'(a)') 'AUXIS  (GEN-A2*)'
          else if (basis .eq. 'D' ) then
            write(30,'(a)') 'BASIS  (pcSseg-0)'
            write(30,'(a)') 'AUXIS  (GEN-A2*)'
          else
            write(0,*) 'Bad basis for demon: ', basis
            stop 1
          endif

          if( demon5 ) then
            write(30,'(a)') 'NMR READ SHIELDING'
          else
            write(30,'(a)') 'NMR READ'
          endif
          do i = 1,nhighatom
            write(30,'(a,a)', advance='no' ) dlabel(i),' '
            if( mod(i,12).eq.0 .and. i.ne.nhighatom ) write(30,'(a)') ' &'
          end do
          write(30,'(a)') ' '
          write(30,'(a)') 'PRINT NMR'

          if( qopt ) then
            write(30,'(a)') 'OPTIMIZE CARTESIAN MAX=5'
            if( nhighatom .lt. iqmprot ) then
               write(30,'(a)') 'CONSTANTS'
               do i=nhighatom+1,iqmprot
                  write(30,'(a,a)') dlabel(i), '  XYZ'
               end do
            endif
          endif

        else if( orca ) then
          write(30,'(a)') '*'
          if( qopt ) then
            write(30,'(a)') '%geom MaxIter=10'
            if( nhighatom .lt. iqmprot ) then
               ! constrain protein/nuc. acid atoms not in the primary residue
               write(30,'(a)') '      Constraints'
               write(30,'(a,i0,a,i0,a)') &
                  '        { C ', nhighatom, ':', iqmprot-1, ' C }'

                write(30,'(a)') '      end'
                write(30,'(a)') '    end'
            endif
          endif
          write(30,'(a)') '%eprnmr  ori GIAO'
          write(30,'(a)', advance='no') '   nuclei = 1'
          do i=2,nhighatom
             write(30,'(a1,i3)', advance='no') ',', i
          end do
          write(30,'(a)') ' {shift}'
          write(30,'(a)') 'end'

        else if ( jaguar ) then
          write(30,'(a)') '&'

        else if ( qchem ) then
          write(30,'(a)') '$end'
          write(30,'(a)') '   '
          write(30,'(a)') '$rem'
          write(30,'(a)') 'MOPROP        1'
          write(30,'(a)') 'SCF_ALGORITHM DIIS'
          if( functional(1:4) == 'OLYP' ) then
             write(30,'(a)') 'EXCHANGE      OPTX'
             write(30,'(a)') 'CORRELATION   LYP'
          else if( functional(1:5) == 'O3LYP' ) then
             write(30,'(a)') 'METHOD O3LYP'
          else
             stop 'bad functional for qchem'
          endif
          write(30,'(a)') 'SCF_CONVERGENCE  7'
          write(30,'(a)') 'THRESH          10'
          write(30,'(a)') 'BASIS         GEN'
          write(30,'(a)') 'PURECART      1111'
          write(30,'(a)') 'SYMMETRY      FALSE'
          write(30,'(a)') 'WAVEFUNCTION_ANALYSIS  FALSE'
          write(30,'(a)') '$end'
          write(30,'(a)') ''

          if( basis .eq. 'D' ) then
              open( UNIT=11, FILE=trim(afnmrhome) // '/basis/pcsseg-0.in')
          else if( basis .eq. 'T' ) then
              open( UNIT=11, FILE=trim(afnmrhome) // '/basis/pcsseg-1.in')
          else if( basis .eq. 'Q' ) then
              open( UNIT=11, FILE=trim(afnmrhome) // '/basis/pcsseg-2.in')
          else
              write(0,*) 'afnmr/qchem only supports seg0, seg1 or seg2 basis sets'
              stop 1
          end if
          rewind(11)
          do kbas=1,9999
             read(11,'(a80)',end = 67) line
             write(30,'(a)') trim(line)
          end do
      67  close(11)

        end if

        ! Again, qopt-only options:
        if ( xtb ) then
          ! write(47,*)  '$opt '
          ! write(47,*)  '   engine=rf'
#if 1   /*  minimize primary residue plus waters */
          if( nhighatom .lt. iqmprot ) then
             write(47,*) '$fix'
             write(47,*) '   atoms: ', nhighatom+1,'-',iqmprot
             write(47,*) '$end'
          end if
#else   /*  minimize just waters */
          write(47,*) '$fix'
          write(47,'(a,i0)') '   atoms: 1-',iqmprot
          write(47,*) '$end'
#endif
          close(47)
          close(44)
        else if ( quick ) then
          write(44,*)
#if 1   /*  minimize primary residue plus waters */
          if( nhighatom .lt. iqmprot ) then
             do i=nhighatom+1,iqmprot
                write(44,'(a,i4)') 'FREEZE ', i
             end do
          end if
#else   /*  minimize just waters */
          if( nhighatom .lt. iqmprot ) then
             do i=1,iqmprot
                write(44,'(a,i4)') 'FREEZE ', i
             end do
          end if
#endif
          write(44,*)
          close(44)
        else if ( terachem ) then 
          if( nhighatom .lt. iqmprot ) then
             write(30,'(a)') '$constraints'
             do i=nhighatom+1,iqmprot
                write(30,'(a,i4)') '  atom ', i
             end do
             write(30,'(a)') '$end'
          end if

        end if

        close(30)
        if( orca ) close(32)
        return
end subroutine finish_program_files

subroutine external_minimizer( cfrag, iqm, kuser )
      use comafnmr
      implicit none
      integer, intent(in) :: cfrag, kuser
      integer, intent(inout) :: iqm
      integer :: i,iline
        ! What follows is done last (for each residue): it does a 
        !  quantum minimization of the fragment (using input files
        !  created above), extracts the minimized coordinates, and 
        !  places them into the fragment .min.pqr and .min.inp files.

        if( xtb ) then
          !  first, fix the xyz file, since we finally know iqm:
          open(44,file=filek//'.xyz1')
          open(35,file=filek//'.xyz')
          write(35,*) iqm
          write(35,*)
          do i=1,iqm
            read(44,'(a)') line
            write(35,'(a)') trim(line)
          end do
          close(44)
          close(35)
          call execute_command_line( '/bin/rm -f ' // filek &
                // '.xyz1' )
          
          !  do the xtb minimization here:
          write(6,*)
          write(6,*) 'Optimize geometry using xtb'
          write(0,*) 'Optimize geometry using xtb for residue', kuser
          write(cfragtxt,'(i4)') cfrag
          commandline = 'xtb ' // filek(1:lengthb+3) &
             // '.xyz --opt --cycles 500 --chrg ' // trim(adjustl(cfragtxt))  &
             // ' --gbsa h2o --input ' // filek(1:lengthb+3) // '_xtb.inp' &
             // ' > ' // filek(1:lengthb+3) // '.xtb.log' 
          write(6,*) trim(commandline)
          call execute_command_line( trim(commandline) )
          call execute_command_line( &
            '/bin/rm -f charges wbo xtbrestart xtbopt.log xtbtopo.mol' )
          call execute_command_line( &
            '/bin/rm -f ' // filek // '_xtb.inp' )
          call execute_command_line( &
            '/bin/rm -f ' // filek // '.xyz' )

          !  extract the coordinates from the xtb output file:
          open(46,file='xtbopt.xyz')
          read(46,*) iqm
          read(46,*)   ! title line
          do i=1,iqm
             read(46,*) dummyl, fxyz(1,i), fxyz(2,i), fxyz(3,i)
          end do
          close(46)
          call execute_command_line( '/bin/rm -f xtbopt.xyz' )
          call transfer_minimized_coords( iqm )
 
        else if( quick ) then

          !  do the quick minimization here:
          write(6,*)
          write(6,*) 'Optimize geometry using quick'
          write(0,*) 'Optimize geometry using quick for residue', kuser
          call execute_command_line( &
             'quick.cuda ' // filek // '_quick.in' )

          !  extract the coordinates from the quick output file:
          open(46,file=filek // '_quick.out')
          do iline=1,100000
             read(46,'(a)') line
             if( line(1:32) .ne. ' OPTIMIZED GEOMETRY IN CARTESIAN' ) cycle
             read(46,*)  ! skip ELEMENT X Y Z line
             do i=1,iqm
                read(46,*) dummyl, fxyz(1,i), fxyz(2,i), fxyz(3,i)
             end do
             exit
          end do
          close(46)
          call transfer_minimized_coords( iqm )
 
        else if( terachem ) then
          write(6,*) 'terachem option for minimization still not ready'
          write(0,*) 'terachem option for minimization still not ready'
          stop
          close(32)
          close(34)
          open(34,file=filek//'.xyz1')
          open(35,file=filek//'.xyz')
          read(34,*)  ! skip dummy first line
          write(35,'(i4)') iqm   ! number of atoms goes on first line
          do i=1,9999
            read(34,'(a)', end=105) line
            write(35,'(a)') trim(line)
          end do
  105     close(34)
          close(35)
          call execute_command_line( '/bin/rm -f ' // filek &
                // '.xyz1' )
          ! TODO: need to call terachem, then transfer the info into demon
        end if
        return

end subroutine external_minimizer
